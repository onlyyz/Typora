## 从Built-In到URP

### 00_URP的组成

[URP的官方文档](https://link.zhihu.com/?target=https%3A//docs.unity3d.com/Packages/com.unity.render-pipelines.universal@7.4/manual/index.html)把URP的组成分成了URP Asset，Renderer Asset，Shaders和相机几个部分。

URP Asset和Render Asset都是用于配置相关参数的Scriptable Object。配置包括是否启用深度缓存、模板缓存、图像质量、渲染器、光照、阴影质量等参数。其中在Forward Renderer Asset中能够启用Render Feature功能，实现之前built-in管线中一些多Pass的效果，有兴趣同学的可以参照这个[外描边实现的样例](https://link.zhihu.com/?target=https%3A//learn.unity.com/tutorial/custom-render-passes-with-urp%235ddc3e79edbc2a002063f672)看看。

URP Shaders默认提供PBR光照（Lit）、简单兰伯特光照（SimpleLit）和无光照（Unlit）等常用Shader，最重要的要属引入了[Shader Graph这一图形化节点材质编辑器](https://link.zhihu.com/?target=https%3A//unity.com/shader-graph)，如果之前使用过Shader Forge 或者ASE的话，很快就能上手。如果这些还不能满足你的需求，需要在URP管线中写ShaderLab，那么可以参考下一篇文章。

### 01_URP的渲染流程

URP采用的是前向渲染管线，对每一帧，URP会执行如下图的[Rendering Loop](https://link.zhihu.com/?target=https%3A//docs.unity3d.com/Packages/com.unity.render-pipelines.universal@7.3/manual/rendering-in-universalrp.html)。一个Rendering Loo中对场景中的每个相机执行相机的Camera Loop。

#### 1.1、从Built-In到URP

URP和之前的Built-In管线有[不少区别](https://link.zhihu.com/?target=https%3A//docs.unity3d.com/Packages/com.unity.render-pipelines.universal@7.4/manual/universalrp-builtin-feature-comparison.html)，对于熟悉了Built-In管线中工作的同学有不少需要适应的地方，SRP Batcher和可使用Pass数量的变化是URP重要的特性。

#### 1.2、SRP Batcher

https://blog.unity.com/engine-platform/srp-batcher-speed-up-your-rendering

熟悉Batcher的同学都知道，合批操作能减少Draw Call，提升渲染效率。

而[SRP Batcher](https://link.zhihu.com/?target=https%3A//blogs.unity3d.com/2019/02/28/srp-batcher-speed-up-your-rendering/)，虽然也叫Batcher，但是工作原理和静态合批与动态合批都不太一样。SRP Batcher通过ConstantBuffer将对象的基本信息存储在GPU上，从而避免了重复提交数据带来的性能损失。

要想使用SRP Batcher，需要在URP Asset中勾选启用，并且要有对应支持的Shder（默认提供的URP Shader和使用Shader Graph生成的Shader都行）。如果是自定义的ShderLab，则需要将需缓存到CBuffer的变量写到CBUFFER_START（UnityPerMaterial）和CBUFFER_END之间，具体可以看下一篇。

```text
CBUFFER_START(UnityPerMaterial)
    float4 _BaseMap_ST;
    float4 _BaseColor;
CBUFFER_END
```

#### 1.3、URP Unlit Shader

在Unity的Project中右键创建的Unlit Shader还是一个Built-in管线的Shader（尽管在URP中也能用）。而如果你需要一个同样功能的URP Unlit Shader，它应该长这个样子。

```glsl
Shader "URPCustom/Unlit"
{
    Properties
    {
        _BaseMap ("Base Texture",2D) = "white"{}
        _BaseColor("Base Color",Color) = (1,1,1,1)
        _SpecularColor("SpecularColor",Color)=(1,1,1,1)
        _Smoothness("Smoothness",float)=10
        _Cutoff("Cutoff",float)=0.5
    }
    SubShader
    {
        Tags
        {
            "RenderPipeline"="UniversalPipeline"//这是一个URP Shader！
            "Queue"="Geometry"
            "RenderType"="Opaque"
        }
        
       HLSLINCLUDE
         //CG中核心代码库 #include "UnityCG.cginc"
        #include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Core.hlsl"
        //除了贴图外，要暴露在Inspector面板上的变量都需要缓存到CBUFFER中
        CBUFFER_START(UnityPerMaterial)
            float4 _BaseMap_ST;
            float4 _BaseColor;
            float4 _SpecularColor;
            float _Smoothness;
            float _Cutoff;
        CBUFFER_END
        ENDHLSL


        Pass
        {
            Tags{"LightMode"="UniversalForward"}//这个Pass最终会输出到颜色缓冲里

            HLSLPROGRAM //CGPROGRAM
            //CG中核心代码库 Lighting.hlsl
            #include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Lighting.hlsl"

            #pragma vertex vert
            #pragma fragment frag

            struct Attributes//这就是a2v
            {
                float4 positionOS : POSITION;
                float4 normalOS : NORMAL;
                float2 uv : TEXCOORD;
                
            };
            struct Varings//这就是v2f
            {
                float4 positionCS : SV_POSITION;
                float2 uv : TEXCOORD;
                float3 positionWS : TEXCOORD1;
                float3 viewDirWS : TEXCOORD2;
                float3 normalWS : TEXCOORD3;
            };

            TEXTURE2D(_BaseMap);//在CG中会写成sampler2D _MainTex;
            SAMPLER(sampler_BaseMap);

            Varings vert(Attributes IN)
            {
                Varings OUT;
                //在CG里面，我们这样转换空间坐标 o.vertex = UnityObjectToClipPos(v.vertex);
                VertexPositionInputs positionInputs = GetVertexPositionInputs(IN.positionOS.xyz);
                OUT.positionCS = positionInputs.positionCS;
                OUT.positionWS = positionInputs.positionWS;
                
                //视线方向（ViewDir）,下面的GetCameraPositionWS()是Lighting.hlsl中获取相机位置的方法
                OUT.viewDirWS = GetCameraPositionWS() - positionInputs.positionWS;

                //法线（NormalDir）：
                VertexNormalInputs normalInputs = GetVertexNormalInputs(IN.normalOS.xyz);
                OUT.normalWS = normalInputs.normalWS;

                ///=====================================================多光源
                //逐顶点计算附加光源
//                half3 vertexLight = VertexLighting(vertexInput.positionWS, normalInput.normalWS);
                
                OUT.uv=TRANSFORM_TEX(IN.uv,_BaseMap);
                return OUT;
            }

            float4 frag(Varings IN):SV_Target
            {
                //在CG里，我们这样对贴图采样 fixed4 col = tex2D(_MainTex, i.uv);
                half4 baseMap = SAMPLE_TEXTURE2D(_BaseMap, sampler_BaseMap, IN.uv);                

                //光线方向（LightDir），GetMainLight()方法能获取主光源的颜色、方向、衰减等信息，非常方便：
                Light light = GetMainLight();
                float3 lightDirWS = light.direction;

                half3 diffuse = baseMap.xyz * _BaseColor * LightingLambert(light.color, light.direction, IN.normalWS);
                //等同于：
                //half3 diffuse = lightColor*saturate(dot(normal, lightDir));

                half3 specular = LightingSpecular(light.color, light.direction, normalize(IN.normalWS), normalize(IN.viewDirWS), _SpecularColor, _Smoothness);
                // 等同于：
                //float3 halfVec = SafeNormalize(float3(lightDir) + float3(viewDir));
                //half NdotH = saturate(dot(normal, halfVec));
                //half3 specular = lightColor * specular.rgb * pow(NdotH, smoothness);

                
                ///=====================================================多光源
                //逐像素计算每个附加光源
                //如果希望每个光源都能够进行逐像素的漫反射和高光反射计算
                //那么我们就需要在片元着色器中遍历每一个光源，并进行和上面一样的光照计算。
                
                //GetAdditionalLightsCount()能够获取到影响这个片段的附加光源数量，但是如果数量超过了URP中设定的附加光照上限，就会返回附加光照上限的数量。
                //GetAdditionalLight(lightIndex, IN.positionWS);方法会按照index去找到对应的光源，并根据提供的片段世界坐标位置计算光照和阴影衰减，并存储在返回的Light结构体内。
                
                uint pixelLightCount = GetAdditionalLightsCount();
                for (uint lightIndex = 0; lightIndex < pixelLightCount; ++lightIndex)
                {
                    Light light = GetAdditionalLight(lightIndex, IN.positionWS);
                    diffuse += LightingLambert(light.color, light.direction, IN.normalWS);
                    specular += LightingSpecular(light.color, light.direction, normalize(IN.normalWS), normalize(IN.viewDirWS), _SpecularColor, _Smoothness);
                }

                half3 color=baseMap.xyz * diffuse * _BaseColor + specular;
                clip(baseMap.a - _Cutoff);
                return float4(color,1);
            }
            ENDHLSL  //ENDCG          
        }
        
        Pass
        {
            Name "ShadowCaster"
            Tags{"LightMode" = "ShadowCaster"}

            ZWrite On
            ZTest LEqual
            Cull[_Cull]

            HLSLPROGRAM
            // Required to compile gles 2.0 with standard srp library
            #pragma prefer_hlslcc gles
            #pragma exclude_renderers d3d11_9x
            #pragma target 2.0

            // -------------------------------------
            // Material Keywords
            #pragma shader_feature _ALPHATEST_ON
            #pragma shader_feature _GLOSSINESS_FROM_BASE_ALPHA

            //--------------------------------------
            // GPU Instancing
            #pragma multi_compile_instancing

            #pragma vertex ShadowPassVertex
            #pragma fragment ShadowPassFragment


            //由于这段代码中声明了自己的CBUFFER，与我们需要的不一样，所以我们注释掉他
            //#include "Packages/com.unity.render-pipelines.universal/Shaders/SimpleLitInput.hlsl"
            //它还引入了下面2个hlsl文件
            #include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Core.hlsl"
            #include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/SurfaceInput.hlsl"
            #include "Packages/com.unity.render-pipelines.universal/Shaders/ShadowCasterPass.hlsl"
            ENDHLSL
        }
    }
}
```